SELECT DISTINCT
  ?house ?houseLabel
  ?legislature ?legislatureLabel
  ?term ?termLabel
  ?termStart ?termEnd
WHERE {
  VALUES ?house { wd:Q2119458 wd:Q2119518 wd:Q54830869 wd:Q54830872 wd:Q54830875 wd:Q54830880 wd:Q54830883 wd:Q54830886 wd:Q54830889 wd:Q54830892 wd:Q54830895 wd:Q54830916 wd:Q54830922 wd:Q54830929 wd:Q54830948 wd:Q54830951 wd:Q54830954 }
  ?house (p:P361/ps:P361)* ?legislature .
      ?baseTerm p:P31|p:P279 [ ps:P279|ps:P31 wd:Q15238777 ; pq:P642 ?legislature ] .
      OPTIONAL { ?subTerm wdt:P31 ?baseTerm }

  BIND(COALESCE(?subTerm, ?baseTerm) AS ?term)

  OPTIONAL { ?term (wdt:P580|wdt:P571) ?termStart. }
  OPTIONAL { ?term (wdt:P582|wdt:P576) ?termEnd. }
  OPTIONAL { ?term (wdt:P155|wdt:P1365) ?termReplaces }
  OPTIONAL { ?term (wdt:P156|wdt:P1366) ?termReplacedBy }

  FILTER (!BOUND(?termEnd) || ?termEnd > NOW())
  FILTER (!BOUND(?termReplacedBy))
  SERVICE wikibase:label { bd:serviceParam wikibase:language "en,es". }
} ORDER BY ?termStart ?term
